# Snakefile for aBSREL-MH Analysis
# @Author: Alexander G Lucaci
# @Description: aBSREL ModelTest and other musings.

# ---------------------------------------------------------------------
# Imports
# ---------------------------------------------------------------------
import os
import sys
import json
import csv
from pathlib import Path
import glob

# ---------------------------------------------------------------------
# Configuration
# ---------------------------------------------------------------------
# Load the cluster json file, has information on submitting jobs.
with open("cluster.json", "r") as in_c:
    cluster = json.load(in_c)
# end with

# Load the configuration yaml file.
configfile: 'config.yml'

# ---------------------------------------------------------------------
# Declares
# ---------------------------------------------------------------------
# Get the important directories
DATA_DIR = config["DataDirectory"]

# Glob all of the files
NEXUS_FILES = glob.glob(os.path.join(DATA_DIR, "*" + config["FileEndingNexus"]))

# Get the basenames, need it for output
NEXUS_Filenames = [os.path.basename(x) for x in NEXUS_FILES]

# Report to user
print("# Number of NEXUS files to process:", len(NEXUS_FILES))

# Get main output directory
OUTDIR = config["OutputDirectory"]

# Set the subdirectories for output.
OUTDIR_aBSRELMH = os.path.join(OUTDIR, "aBSREL-MH")
OUTDIR_aBSREL   = os.path.join(OUTDIR, "aBSREL")
OUTDIR_aBSRELSMH = os.path.join(OUTDIR, "aBSRELS-MH")
OUTDIR_aBSRELS   = os.path.join(OUTDIR, "aBSRELS")

# Report to user
print("# Files for aBSREL-MH and aBSREL will be saved in:", OUTDIR)

# Create output dirs
Path(OUTDIR).mkdir(parents=True, exist_ok=True)
Path(OUTDIR_aBSRELMH).mkdir(parents=True, exist_ok=True)
Path(OUTDIR_aBSREL).mkdir(parents=True, exist_ok=True)
Path(OUTDIR_aBSRELSMH).mkdir(parents=True, exist_ok=True)
Path(OUTDIR_aBSRELS).mkdir(parents=True, exist_ok=True)

# Settings, these can be passed in or set in a config.json type file
PPN = cluster["__default__"]["ppn"] 
hyphy = "hyphy"

print("# We will use", PPN, "processors")

# ==============================================================================
# Helper functions
# ==============================================================================

def assign_code(wildcards):
    if wildcards.sample == "COXI.nex":
        return "Vertebrate-mtDNA"
    return "Universal"
#end method


# ---------------------------------------------------------------------
# Rule all
# ---------------------------------------------------------------------
rule all:
    input:
        expand(os.path.join(OUTDIR_aBSRELMH, "{NEXUS}.aBSREL-MH.json"), NEXUS=NEXUS_Filenames),
        expand(os.path.join(OUTDIR_aBSREL, "{NEXUS}.aBSREL.json"), NEXUS=NEXUS_Filenames),
        expand(os.path.join(OUTDIR_aBSRELSMH, "{NEXUS}.aBSRELS-MH.json"), NEXUS=NEXUS_Filenames),
        expand(os.path.join(OUTDIR_aBSRELS, "{NEXUS}.aBSRELS.json"), NEXUS=NEXUS_Filenames)
    #end input
#end rule

# ---------------------------------------------------------------------
# Individual rules
# ---------------------------------------------------------------------

rule aBSRELSMH:
    input:
        nexus = os.path.join(DATA_DIR, "{sample}")
    output:
        output = os.path.join(OUTDIR_aBSRELSMH, "{sample}.aBSRELS-MH.json")
    params:
        code=assign_code
    shell:
        "mpirun -np {PPN} {hyphy} aBSREL --alignment {input.nexus} --output {output.output} --multiple-hits Double+Triple --srv Yes --code {params.code}"
    #end shell
#end rule 

rule aBSRELMH:
    input:
        nexus = os.path.join(DATA_DIR, "{sample}") 
    output:
        output = os.path.join(OUTDIR_aBSRELMH, "{sample}.aBSREL-MH.json")
    params:
        code=assign_code
    shell:
        "mpirun -np {PPN} {hyphy} aBSREL --alignment {input.nexus} --output {output.output} --multiple-hits Double+Triple --code {params.code}"
    #end shell
#end rule

rule aBSREL:
    input:
        nexus = os.path.join(DATA_DIR, "{sample}") 
    output:
        output = os.path.join(OUTDIR_aBSREL, "{sample}.aBSREL.json")
    params:
        code=assign_code
    shell:
        "mpirun -np {PPN} {hyphy} aBSREL --alignment {input.nexus} --output {output.output} --code {params.code}"
    #end shell
#end rule

rule aBSRELS:
    input:
        nexus = os.path.join(DATA_DIR, "{sample}") 
    output:
        output = os.path.join(OUTDIR_aBSRELS, "{sample}.aBSRELS.json")
    params:
        code=assign_code
    shell:
        "mpirun -np {PPN} {hyphy} aBSREL --alignment {input.nexus} --output {output.output} --srv Yes --code {params.code}"
    #end shell
#end rule


# ---------------------------------------------------------------------
# End of file
# ---------------------------------------------------------------------
